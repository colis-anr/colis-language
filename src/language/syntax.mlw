
(** Non-negative integers *)
module Nat

  use int.Int

  type nat = { nat : int }
    invariant { nat >= 0 }

  let function mk_nat (i: int)
    requires { i >= 0 }
    ensures { result.nat = i }
  = { nat = i }
end

(** Identifiers of variables and functions *)
module Identifier

  use mystring.String

  type identifier

  val function identifier_eq (v1 v2:identifier) : bool
    ensures { result <-> v1 = v2 }

  val function identifier_to_string identifier : string
end

(** Abstract syntax of the CoLiS language *)
module Syntax

  use list.List
  use option.Option

  use mystring.String
  use export Nat
  use export Identifier

  (** Indicates if a string in a list expression should be split (on default IFS=`[ \t\n]`) *)
  type split = Split | DontSplit

  (** A `string_expression` evaluates to a `string` *)
  type string_expression =                        (** CoLiS       | Shell                                 *)
    | SLiteral string                             (** 'literal'   | 'string'                              *)
    | SVariable identifier                        (** id          | ${id}                                 *)
    | SSubshell instruction                       (** embed instr | $(instruction)                        *)
    | SConcat string_expression string_expression (** expr expr   | expression expression (without space) *)
    | SArgument nat                               (** arg N       | $N                                    *)

  (** A list_expression` evaluates to a `list string` *)
  with list_expression =                          (* [split? expr, ...] | "expr" OR expr *)
    list (string_expression, split)

  (* Allow $? only as return code, cf. NOTES[Exit] *)
  with return_code = (** CoLiS    | Shell *)
    | RSuccess       (** success  | 0     *)
    | RFailure       (** failure  | 1-255 *)
    | RPrevious      (** previous | $?    *)

  (** TODO add utilities `true`, `false` to *Specification of UNIX commands*  *)
  with instruction =                                   (* ColLiS                         | Shell                            *)
    | IAssignment identifier string_expression         (* id := sexpr                    | id=sexpr                         *)
    | ISequence instruction instruction                (* instr; instr                   | instr; instr                     *)
    | ISubshell instruction                            (* process instr                  | ( instr )                        *)
    | IIf instruction instruction instruction          (* if instr then instr else instr | if instr; then instr; else instr *)
    | INot instruction                                 (* not instr                      | ! instr (see NOTES[Conditionals])*)
    | IPipe instruction instruction                    (* pipe instr into instr epip     | instr | instr                    *)
    | IWhile instruction instruction                   (* while instr do instr done      | while instr; do instr; done      *)
    | INoOutput instruction                            (* nooutput instr                 | instr > /dev/null                *)
    | IForeach identifier list_expression instruction  (* for x in lexpr do instr done   | for x in words; do instr; done   *)
    | ICallUtility identifier list_expression          (* id lexpr?                      | words                            *)
    | ICallFunction identifier list_expression         (* call id lexpr?                 | words                            *)
    | IReturn return_code                              (* return r                       | return r                         *)
    | IExit return_code                                (* exit r                         | exit r (see NOTES[Exit])         *)
    | IShift (option nat)                              (* shift n?                       | shift n?                         *)

  with function_definition = (identifier, instruction) (* function id instr; | function id instr *)

  with program = {
    function_definitions: list function_definition;
    instruction: instruction
  }
end
